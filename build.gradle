plugins {
  id 'scala'
  id 'com.github.johnrengelman.shadow' version '1.2.1'
}

repositories {
  jcenter()
  ivy {
    url "http://mirror.nexcess.net"
    layout "pattern", {
      artifact "[organisation]/[module]-[revision]/[module]-[revision]-[classifier].[ext]"
      m2compatible = true
    }
  }
}

configurations {
  provided
  spark
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.useAnt = false
  scalaCompileOptions.useCompileDaemon = true
}

sourceSets {
  main.compileClasspath += configurations.provided
  test.compileClasspath += configurations.provided
  test.runtimeClasspath += configurations.provided
}

dependencies {
  provided 'org.apache.spark:spark-core_2.10:1.4.0'
  provided 'org.scala-lang:jline:2.10.5'

  spark(group: 'apache.spark', name: 'spark', version: '1.4.0') {
    artifact {
      name = 'spark'
      type = 'tgz'
      classifier = 'bin-hadoop2.4'
      extension = 'tgz'
    }
  }

  testCompile 'junit:junit:4.12'
  testCompile 'org.scalatest:scalatest_2.10:2.2.5'
}

def sparkBaseDir = new File(buildDir, 'spark')

RelativePath stripParent(RelativePath p) {
  return new RelativePath(p.endsWithFile, p.segments[1..-1] as String[])
}

task setupSpark(type: Copy) {
  from tarTree(configurations.spark.singleFile)
  into sparkBaseDir
  eachFile { it.relativePath = stripParent(it.relativePath) }
  includeEmptyDirs false
}

tasks.withType(Test) {
  dependsOn setupSpark
  environment.SPARK_HOME = sparkBaseDir.absolutePath
}
